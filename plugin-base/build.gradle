plugins {
    id 'java-library'
}

group 'io.everytrade'
version projectVersion

configurations {
    pluginCompile
    pluginRuntime
    fatJar {
        extendsFrom pluginCompile
        extendsFrom pluginRuntime
        exclude group: "org.slf4j"
    }
    compileClasspath {
        extendsFrom pluginCompile
    }
    testImplementation {
        extendsFrom pluginCompile
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    annotationProcessor "org.pf4j:pf4j:$pf4jVersion"

    compileOnly 'org.projectlombok:lombok:1.18.22'

    compileOnly project(':plugin-api')
    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    pluginCompile "com.univocity:univocity-parsers:2.8.4"
    pluginCompile "org.knowm.xchange:xchange-binance:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bittrex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitstamp:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitfinex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitmex:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-bitflyer:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-core:$xchangeVersion"
    pluginCompile "org.knowm.xchange:xchange-huobi:$xchangeVersion"

    pluginCompile "com.github.charvam:V3-Open-API-SDK:db22182525f075bfb8999bcdbea4a132e4d933cf"

    /*
     * only for temporary use till pull request confirmation https://github.com/knowm/XChange/pull/4402
     */
    //    pluginCompile "org.knowm.xchange:xchange-coinmate:$xchangeVersion"
    pluginCompile "com.github.martinkyov.Xchange:xchange-coinmate:f80f587e10faaf92839055a7e6154dd509c7d0c4"

    /*
     * only for temporary use till pull request confirmation https://github.com/knowm/XChange/pull/4448
     */
    //    pluginCompile "org.knowm.xchange:xchange-kraken:$xchangeVersion"
    pluginCompile "com.github.martinkyov.Xchange:xchange-kraken:54464694ae69d0815f35b4395823eac3d6301a9d"


    /*
     * only for temporary use till pull request confirmation https://github.com/knowm/XChange/pull/4403
     */
    //    pluginCompile "org.knowm.xchange:xchange-coinbase:$xchangeVersion"
    // Temporary merged issue ETD-191 and ETD-235 CoinbaseConnectorFixingAndAddingTransfers
    // ETD:235 pluginCompile "com.github.martinkyov.Xchange:xchange-coinbase:beb42644b59a486126fe7eefde69729d4c3e78a8"
    pluginCompile "com.github.martinkyov.Xchange:xchange-coinbase:136289732f459f489caa0cd4860f6dcd3b76712f"

    /*
     * only for temporary use till pull request confirmation https://github.com/knowm/XChange/pull/4412
     */
    //   pluginCompile "org.knowm.xchange:xchange-coinbasepro:$xchangeVersion"
    pluginCompile "com.github.martinkyov.Xchange:xchange-coinbasepro:e4303df2a1ca9a055219a0aa6d57603707103140"

    pluginCompile "com.github.GENERALBYTESCOM:bitrafael_public:1.0.47"

    //TODO Remove it once ResCU releases a newer version with updated dependency - due to security bugs in the older
    // version used by ResCU
    pluginCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.5.1'

    pluginCompile("com.github.GENERALBYTESCOM.batm_public:server_extensions_extra:0a1330d6e14a79e6f45be0c8096ea2698c558ed2") {
        exclude group: 'com.github.GENERALBYTESCOM.bitrafael_public', module: 'bitrafael-client'
        exclude group: 'org.web3j', module: 'core'
        exclude group: 'org.knowm.xchange'
        exclude group: 'com.google.protobuf'
    }


    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation project(':plugin-api')
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

jar {
    manifest {
        attributes('Plugin-Id': 'everytrade-base')
        attributes('Plugin-Version': projectVersion)
        attributes('Plugin-Requires': requiredEverytradeVersion) //required Everytrade version
        attributes('Plugin-Description': 'basic integrations plugin')
        attributes('Plugin-Provider': 'Everytrade.io')
        attributes('Plugin-License': 'GNU GPL 2 with Classpath Exception')
    }
    from {
        configurations.fatJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude "META-INF/*.SF"
    exclude "META-INF/*.RSA"
    exclude "META-INF/*.DSA"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}